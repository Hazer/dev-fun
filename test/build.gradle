apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode project.versionCode
        versionName project.versionName

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        javaCompileOptions {
            annotationProcessorOptions {
                // We're not using any annotation processors at compile time
                // but it complains since we reference the compiler project
                includeCompileClasspath false
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        test.java.srcDirs += 'src/testData/kotlin'
    }
}

dependencies {
    // Dev Fun
    testCompile project(':devfun-compiler')
    testCompile project(':devfun')

    // TestNG - http://testng.org
    testCompile 'org.testng:testng:6.11'

    // Logging - https://logback.qos.ch
    testCompile 'ch.qos.logback:logback-core:1.2.1'
    testCompile 'ch.qos.logback:logback-classic:1.2.1'

    // Kotlin
    testCompile "org.jetbrains.kotlin:kotlin-annotation-processing:$kotlinVersion"
    testCompile "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlinVersion"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    // Mockito - http://mockito.org/
    testCompile 'org.mockito:mockito-inline:2.7.14'
    testCompile 'com.nhaarman:mockito-kotlin:1.3.0'

    // Javac Tools
    def toolsJar = new File("${System.properties['java.home']}/../lib/tools.jar")
    if (!toolsJar.exists()) {
        throw new RuntimeException("Tools jar not found at ${toolsJar.canonicalPath}")
    }
    testCompile files(toolsJar.canonicalPath)

    // Full Java
    def bootJars = System.properties.get('sun.boot.class.path').toString().split(File.pathSeparator)
    def jars = new ArrayList<File>()
    for (String path : bootJars) {
        if (path.endsWith("rt.jar")) {
            jars += new File(path)
        }
    }
    testCompile files(jars)
}

// Configure TestNG
project.afterEvaluate {
    def tests = project.getTasksByName("testDebugUnitTest", false) + project.getTasksByName("testReleaseUnitTest", false)
    tests.forEach {
        it.doFirst {
            useTestNG()
            testLogging.showStandardStreams = true
        }
    }
}
