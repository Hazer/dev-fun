apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

project.doConfigureAndroidLibrary(this, false)

dependencies {
    // Kotlin
    implementation project.kotlinStdLib

    // Support libs
    implementation project.supportAppCompat

    // DevFun
    implementation project(':devfun')
    implementation project(':devfun-annotations')
    implementation project(':devfun-compiler')
    implementation project(':devfun-httpd')
    implementation project(':devfun-httpd-frontend')
    implementation project(':devfun-inject-dagger2')
    implementation project(':devfun-menu')
    implementation project(':devfun-stetho')
    implementation project(':devfun-util-glide')
    implementation project(':devfun-util-leakcanary')
//    compile fileTree(new File("${rootProject.project(':demo').buildDir}/libs/jarForDokka.jar"))
}

apply plugin: 'org.jetbrains.dokka-android'
dokka {
    if (file('Module.md').exists()) {
        includes = ['Module.md']
    }

    moduleName = 'gh-pages'
    outputFormat = 'gfm'
    outputDirectory = '.'
    dokkaFatJar = project.dokkaFatJar
}

project.afterEvaluate {
    def mainDokkaTask = project.getTasksByName('dokka', false).first()
    rootProject.getTasksByName('dokka', true).forEach { libDokkaTask ->
        mainDokkaTask.sourceDirs += libDokkaTask.project.isAndroid ?
                libDokkaTask.project.android.sourceSets.main.java.srcDirs :
                libDokkaTask.project.sourceSets.main.allSource

        libDokkaTask.dependsOn 'assemble'

        mainDokkaTask.linkMapping {
            dir = "$rootDir/${libDokkaTask.project.name}/src/main/java"
            url = "https://github.com/NextFaze/dev-fun/tree/master/${libDokkaTask.project.name}/src/main/java"
            suffix = "#L"
        }

        if (libDokkaTask != mainDokkaTask) {
            mainDokkaTask.dependsOn libDokkaTask
        }
    }

    mainDokkaTask.dependsOn clearDokkaTask

    // WIP - we don't actually want this in docs though (only source linking to repo)...
//    // Add demo
//    def demoProject = rootProject.project(':demo')
//    mainDokkaTask.sourceDirs += demoProject.android.sourceSets.main.java.srcDirs
//    mainDokkaTask.linkMapping {
//        dir = "$rootDir/${demoProject.name}/src/main/java"
//        url = "https://github.com/NextFaze/dev-fun/tree/master/${demoProject.name}/src/main/java"
//        suffix = "#L"
//    }
//
//    mainDokkaTask.sourceDirs += demoProject.android.sourceSets.debug.java.srcDirs
//    mainDokkaTask.linkMapping {
//        dir = "$rootDir/${demoProject.name}/src/debug/java"
//        url = "https://github.com/NextFaze/dev-fun/tree/master/${demoProject.name}/src/debug/java"
//        suffix = "#L"
//    }
}
