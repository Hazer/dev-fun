plugins {
    id("kotlin")
    kotlin("kapt")
}

dependencies {
    // DevFun
    compileOnly(project(":devfun-compiler")) // we only reference constants (DevFunProcessor's supported options)

    // Gradle
    compileOnly(gradleApi())

    // Kotlin
    compileOnly(Dependency.kotlin.stdLib)
    compileOnly(Dependency.kotlin.gradlePlugin)
    compileOnly(Dependency.kotlin.gradlePluginApi)

    // Android
    compileOnly(Dependency.androidPlugin) {
        exclude(group = "org.jetbrains.kotlin")
    }

    // Google AutoService - https://github.com/google/auto/tree/master/service
    kapt(Dependency.autoService)
    compileOnly(Dependency.autoService)
}

configureDokka()

afterEvaluate {
    val code = """/** Generated by build script - do not modify directly. */
package com.nextfaze.devfun.gradle.plugin

internal const val versionName = "$version"
"""

    val versionKtFile = File("$projectDir/src/main/kotlin/com/nextfaze/devfun/gradle/plugin/version.kt")
    if (versionKtFile.readText() != code) {
        versionKtFile.writeText(code)
        println("Grade plugin version.kt updated to:\n$code")
    }
}
